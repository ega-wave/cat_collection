.file "cat.c"
.text
.section .text.startup,"ax",@progbits

/*
int _start()
{
*/
.globl _start
.type  _start, @function

_start:
.init:
  pushq %rbp
  leaq write_buf(%rip), %rbp # rbp = write_buf
  pushq %rbx
  pushq %rcx
  xorl %ebx, %ebx
  incl %ebx       # ebx = 1
 
/*
rax rbx rcx rdx rdi rsi rbp
    1   rc  cnt     buf wb
                        u/
                        /wb
    u/
        u/
    u/
    0/1
*/

.loop:
.read:
  movl $131072, %edx # edx = 131072
  leaq read_buf(%rip), %rsi # rsi = read_buf
  xorl %edi, %edi    # edi = 0
  xorl %eax, %eax    # syscall_id = 0 (read)
  syscall            # rax = read(edi, rsi, edx);

  testl %eax, %eax   # eax == 0
  je .exit0          # if true, then goto .exit0
                     # else, fall-through
/*
rax  rbx rcx rdx rdi rsi rbp
     1   rc  cnt     buf wb
             /13
                     /rb
                 u/0
u/0
0/rc         13/ 0/  rb/
rc/
*/

.memcpy:
  movq %rbp, %rdi # rdi = rbp(=write_buf)
  movl %eax, %ecx # ecx = eax(=read_count)
  rep movsb       # *rdi++ = *rsi++ for ecx
/*
rax  rbx rcx   rdx rdi rsi rbp
     1   rc    cnt     buf wb
                   /wb     wb/
rc/      /rc
         rc/rc     wb/ rb/
*/

.write:
  movl %eax, %edx # edx = eax(=read_count)
  movq %rbp, %rsi # rsi = rbp(=write_buf)
  movl %ebx, %edi # edi = 1
  movl %ebx, %eax # eax = 1
  syscall         # rax = write(edi, rsi, rdx);
  cmpl %eax, %edx # edx - eax
  je .loop        # if equal, then goto .loop
                  # else, fall-through
/*
rax  rbx rcx   rdx rdi rsi rbp
     1   rc    cnt     buf wb
rc/            /rc
                       /wb wb/
     1/            /1
/1   1/
1/wc           rc/ 1/  wb/
wc/            rc/
*/

.exit1:
  movl %ebx, %eax
  jmp .finish

.exit0:
  xorl %eax, %eax

.finish:
  popq %rdx
  popq %rbx
  popq %rbp

.exit:
  movl %eax,%edi
  movb $60, %al
  syscall
.size _start, .-_start
/*
}
*/

.globl write_buf
.bss
.align 32
.type write_buf, @object
.size write_buf, 131072
write_buf:
.zero 131072

.globl read_buf
.align 32
.type read_buf, @object
.size read_buf, 131072
read_buf:
.zero 131072

.section .note.GNU-stack,"",@progbits

